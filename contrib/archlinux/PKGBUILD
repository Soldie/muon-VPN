# Maintainer:  Xiaoxiao <i@pxx.io>

pkgname=muon-git
pkgver=r
pkgrel=1
pkgdesc='A fast stateless VPN with simple obfuscation'
arch=('x86_64' 'arm')
url='https://github.com/puxxustc/muon'
license=('GPL3')
depends=('iproute2' 'libmill' 'libsodium' 'lz4')
makedepends=('git')
provides=('muon')
source=('git+https://github.com/puxxustc/muon.git')
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/muon"
    echo "r$(git rev-list --count HEAD)"
}

build() {
    cd "${srcdir}/muon"
    autoreconf -if
    ./configure --prefix=/usr --sysconfdir=/etc
    make
}

check() {
    cd "${srcdir}/muon"
    make check
}

package() {
    cd "${srcdir}/muon"
    make DESTDIR="${pkgdir}" install
}
